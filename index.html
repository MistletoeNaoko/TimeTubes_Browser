<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>TimeTubes Web Version</title>
    <script type="text/javascript" src="js/jquery.min.js"></script>
    <script type="text/javascript" src="js/bootstrap.min.js"></script>
    <script type="text/javascript" src="js/three.min.js"></script>
    <script type="text/javascript" src="js/d3.min.js"></script>
    <script type="text/javascript" src="js/OrbitControls.js"></script>
</head>
<body>
<input type="file" id="fileUpload" onchange="loadFile()"><input id="Button1" type="button" value="Read" onclick="OnButtonClick()"/>
<div id="WebGL-TimeTubes" onresize="onResize()"></div>
<div id="scatterplots"></div>
<div id="output"></div>
<script type="text/javascript" src="js/loadFile.js"></script>
<script type="text/javascript" src="js/TimeTubes.js"></script>
<!--<script type="x-shader/x-vertex" src="shader/ShaderV.glsl" id="ShaderV"></script>-->
<script type="x-shader/x-vertex" id="vertexShaderSimple">
#define MAX_DATASIZE 500

precision mediump float;

varying vec3 vNormal;
varying vec3 vWorldPosition;
varying vec2 vColor;
varying float vDisFlag;

uniform int size;
uniform vec3 points[MAX_DATASIZE];
uniform vec2 radiuses[MAX_DATASIZE];
uniform vec2 colors[MAX_DATASIZE];

float CatmullRom( float P0_, float P1_, float P2_, float P3_, float T_ )
{
    return ( ( ( -0.5 * P0_ + 1.5 * P1_ - 1.5 * P2_ + 0.5 * P3_ ) * T_
            + P0_ - 2.5 * P1_ + 2.0 * P2_ - 0.5 * P3_ ) * T_
            - 0.5 * P0_ + 0.5 * P2_ ) * T_
            + P1_;
}

vec3 InterpPos(int idx, float del)
{
    // Calculate interpolated values of QI and UI from the z value
    vec3 result;
//    int idx = 0;

    // Find out the index of starting point
//    for (int i = 0; i < MAX_DATASIZE; i++) {
//        if (points[i].z <= zPos && zPos < points[i + 1].z) {
//            idx = i;
//            break;
//        }
//    }


    result.x = CatmullRom(points[idx].x, points[idx + 1].x, points[idx + 2].x, points[idx + 3].x, del);
    result.y = CatmullRom(points[idx].y, points[idx + 1].y, points[idx + 2].y, points[idx + 3].y, del);
    result.z = CatmullRom(points[idx].z, points[idx + 1].z, points[idx + 2].z, points[idx + 3].z, del);

    return result;
}

vec2 InterpRad(int idx, float del)
{
    vec2 result;

    result.x = CatmullRom(radiuses[idx].x, radiuses[idx + 1].x, radiuses[idx + 2].x, radiuses[idx + 3].x, del);
    result.y = CatmullRom(radiuses[idx].y, radiuses[idx + 1].y, radiuses[idx + 2].y, radiuses[idx + 3].y, del);

    return result;
}

vec2 InterpColor(int idx, float del)
{
    vec2 result;

    result.x = CatmullRom(colors[idx].x, colors[idx + 1].x, colors[idx + 2].x, colors[idx + 3].x, del);
    result.y = CatmullRom(colors[idx].y, colors[idx + 1].y, colors[idx + 2].y, colors[idx + 3].y, del);

    return result;
}

void main()
{
    // Pass information on the pixel to the fragment shader to add shade
    vNormal = normalMatrix * normal;
    vec4 worldPosition = modelMatrix * vec4(position, 1.0);
    vWorldPosition = worldPosition.xyz;

    // Compute interpolated values for the current pixel
    float Tx = floor(position.z);
    int   Ti = int(Tx);
    float Td = position.z - Tx;
    vDisFlag = 1.0;
    if (Ti >= size - 1)
        vDisFlag = 0.0;
    vec3 currentPos = InterpPos(Ti, Td);
    vec2 currentRad = InterpRad(Ti, Td);
    vColor = InterpColor(Ti, Td);

    // Transform the straight tube based on the data
    vec3 P;
    P.x = currentPos.x + currentRad.x * position.x;
    P.x = -1.0 * P.x;
    P.y = currentPos.y + currentRad.y * position.y;
    P.z = currentPos.z;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(P, 1.0);    // Transform: Camera coordinate -> Local coordinate -> transform
}
</script>

<script id="fragmentShader" type="x-shader/x-fragment">
#define MAX_DATASIZE 500

precision mediump float;

varying vec3 vNormal;
varying vec3 vWorldPosition;
varying vec2 vColor;
varying float vDisFlag;

uniform vec3 lightPosition;
uniform vec2 colors[MAX_DATASIZE];
uniform vec2 minmaxVJ;
uniform vec2 minmaxFlx;
uniform sampler2D texture;

void main()
{
    // Discard pixels out of index
    if (vDisFlag < 1.0)
        discard;

    // Discard the front side of the tube
    if (vWorldPosition.z < 0.0)
        discard;
    vec3 lightDirection = normalize(lightPosition - vWorldPosition);
    vec2 T;
    T.x = (vColor.x - minmaxVJ.x) / (minmaxVJ.y - minmaxVJ.x);
    T.y = (vColor.y - minmaxFlx.x) / (minmaxFlx.y - minmaxFlx.x);
    T.y = 1.0 - T.y;
    vec4 resultColor = texture2D(texture, T);
    resultColor.a = 1.0;
    float c = max(0.0, dot(vNormal, lightDirection)) * 0.3;
    gl_FragColor = vec4(resultColor.r + c, resultColor.g + c, resultColor.b + c, 1.0);
}
</script>
</body>
</html>
